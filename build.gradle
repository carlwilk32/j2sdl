plugins {
    id 'java-library'
    id 'maven-publish'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/carlwilk32/j2sdl")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'net.java.dev.jna:jna:5.13.0'
    api 'org.jetbrains:annotations:23.0.0'
    api 'org.slf4j:slf4j-api:1.7.36'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'net.java.dev.jna:jna-platform:5.11.0'
    testImplementation 'ch.qos.logback:logback-classic:1.4.12'
}

group = 'com.github.carlwilk32'
version = '1.1-SNAPSHOT'

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}
